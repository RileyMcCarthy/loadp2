CON
		rxpin = 63
		FLAGBIT_ZERO = $1		' if set, zero HUB memory
		FLAGBIT_PATCHED = $2		' if set, clock frequency was patched into binary
		
DAT		org

begin		hubset	clkmode_			'set up oscillator 
                waitx   ##25_000_000/100        'wait >10 ms
                or      clkmode_, #3             'enable XI+PLL mode
                hubset  clkmode_                 'enable oscillator
		test	flagbits, #FLAGBIT_ZERO wz
	if_nz	jmp	#skipzero
		'' zero out memory
		wrfast	#0,0
		mov	pb, ##$7c0000/16
zeroloop
		wflong	zeros
		wflong	zeros
		wflong	zeros
		wflong	zeros
		djnz	pb, #zeroloop
		rdfast	#0,#0			' wait for last byte to be written
skipzero
		wrfast	#0,address		'ready to write entire memory starting at address
		setse1	#%010_000000 | rxpin	'select negative edge on p63
		nop		       		' give time to settle
		
mainloop	pollse1				'clear edge detector
		waitse1				'wait for start bit
		waitx	waitbit1_5		'wait for middle of 1st data bit

		rep	@loopend,#8		'get 8 bits
		testp	#rxpin		wc	'sample rx
		rcr	rxbyte,#1		'rotate bit into byte
		waitx	waitbit			'wait for middle of nth data bit
loopend
		shr	rxbyte,#32-8		'justify received byte
		wfbyte	rxbyte			'write to hub

		djnz	filesize,#mainloop	'loop until all bytes received

                rdfast  #0,#0                   'wait for last byte to be written

		'' short pause for host to change baud
		waitx	##25_000_000/10
		
		' switch back to rcfast mode, maybe
		' if the binary was patched with -PATCH then we skip this
		test	flagbits, #FLAGBIT_PATCHED wz
	if_z	jmp	#start_cog		 ' patched, so start right now

		' go back to rcfast mode
		andn	 clkmode_, #3
		hubset	 clkmode_
		waitx	 ##25_000_000/100
		hubset	 #0
start_cog
		coginit	#0,address		'launch cog 0 from address

zeros
		long	0
		
		orgf	$80
clkmode_	res	1			'clock mode
waitbit1_5	res	1			'1.5 bit cycles = 3*clock_freq/baud_rate/2 - 6
waitbit		res	1			'1 bit cycles = clock_freq/baud_rate - 6
filesize	res	1			'binary file size in bytes
address		res	1			'starting address
flagbits	res	1			'flag bits, see definitions above
rxbyte		res	1			'received byte
